globals
    constant integer ELF_CITIZEN_MALE       = 'n00O'
    constant integer ELF_CITIZEN_FEMALE     = 'n00P'
    constant integer ELF_HOUSING            = 'h00S'
endglobals

//===========================================================================
// Calculate the modulus/remainder of (dividend) divided by (divisor).
// Examples:  18 mod 5 = 3.  15 mod 5 = 0.  -8 mod 5 = 2.
//
function ModuloInteger takes integer dividend, integer divisor returns integer
    local integer modulus = dividend - (dividend / divisor) * divisor

    // If the dividend was negative, the above modulus calculation will
    // be negative, but within (-divisor..0).  We can add (divisor) to
    // shift this result into the desired range of (0..divisor).
    if (modulus < 0) then
        set modulus = modulus + divisor
    endif

    return modulus
endfunction

function ChooseHeroSkill takes nothing returns integer
    local integer curHero = GetHeroId()
    local integer level = GetHeroLevelAI()
    local integer mod = ModuloInteger(level, 4)
    local boolean skillUlti = ModuloInteger(level, 6) == 0 and level / 6 <= 9

    if (curHero == DEMON_HUNTER) then
        if (skillUlti) then
            return METAMORPHOSIS
        elseif (mod == 0) then
            return EVASION
        elseif (mod == 1) then
            return MANA_BURN
        elseif (mod == 2) then
            return IMMOLATION
        endif
    elseif (curHero == KEEPER) then
        if (skillUlti) then
            return TRANQUILITY
        elseif (mod == 0) then
            return THORNS_AURA
        elseif (mod == 1) then
            return FORCE_NATURE
        elseif (mod == 2) then
            return ENT_ROOTS
        endif
    elseif (curHero == MOON_CHICK) then
        if (skillUlti) then
            return STARFALL
        elseif (mod == 0) then
            return SCOUT
        elseif (mod == 1) then
            return TRUESHOT
        elseif (mod == 2) then
            return SEARING_ARROWS
        endif
    elseif (curHero == WARDEN) then
        if (skillUlti) then
            return VENGEANCE
        elseif (mod == 0) then
            return SHADOW_TOUCH
        elseif (mod == 1) then
            return FAN_KNIVES
        elseif (mod == 2) then
            return BLINK
        endif
    endif

    return 'Aamk'
endfunction

function ConfigureAI takes nothing returns nothing
    call SetTargetHeroes( true )
    call SetUnitsFlee( true )
    call SetHeroesFlee( true )
    call SetPeonsRepair( true )
    call SetHeroesBuyItems( true )
    call SetHeroesTakeItems( true )
    call SetSlowChopping( false )
endfunction

function BuildingStrategy takes nothing returns nothing
    // **********************************
    // *      Building Strategy         *
    // **********************************
    // Tier 1 Buildings
    call SetReplacements( 1, 2, 3 )
    call SetBuildUnit( 1, TREE_LIFE )
    call SetBuildUnit( 10, WISP )
    call SetBuildUnit( 2, ELF_CITIZEN_MALE )
    call SetBuildUnit( 3, MOON_WELL )
    call SetBuildUnit( 3, ELF_HOUSING )
    call SetBuildUnit( 2, ANCIENT_WAR )
    call SetBuildUnit( 1, ELF_ALTAR )
    call SetBuildUnit( 1, HUNTERS_HALL )
    call SetBuildUnit( 1, DEN_OF_WONDERS )
    call CampaignDefenderEx( 2, 2, 3, ARCHER )
    call CampaignDefenderEx( 1, 1, 1, HUNTRESS )

    // Tier 2 buildings
    call SetBuildUnit( 1, TREE_AGES )
    call SetBuildUnit( 1, HUNTERS_HALL )
    call SetBuildUnit( 1, ANCIENT_WIND )
    call SetBuildUnit( 1, ANCIENT_LORE )
    call SetBuildUnit( 4, MOON_WELL )
    call SetBuildUnit( 5, WISP )
    call SetBuildUnit( 4, ELF_CITIZEN_FEMALE )
    call SetBuildUnit( 4, ANCIENT_PROTECT )
    call SetBuildUpgr( 1, UPG_STR_WILD )
    call SetBuildUpgr( 1, UPG_ULTRAVISION )

    // Tier 3 buildings
    call SetBuildUnit( 1, TREE_ETERNITY )
    call SetBuildUnit( 1, TOTEM )
    call SetBuildUnit( 5, MOON_WELL )
    call SetBuildUnit( 4, ANCIENT_PROTECT )
    call SetBuildUnit( 2, ELF_CITIZEN_MALE )
    call SetBuildUnit( 3, ELF_HOUSING )
    // **********************************
    // *    End Building Strategy       *
    // **********************************
endfunction

//===========================================================================
// Basic attack functionality
//===========================================================================
function AttackTarget takes unit target, boolean addAlliance returns nothing
    if (target == null) then
        return
    endif
    if (addAlliance) then
        call SetAllianceTarget( target )
    endif
    call FormGroup( 3, true )
    call AttackMoveKillA( target )
    if (not addAlliance) then
        call SetAllianceTarget( null )
    endif
endfunction

//===========================================================================
// Initiates an attack based on target priorities
//===========================================================================
function LaunchAttack takes nothing returns nothing
    local unit target = null
    local boolean setAlly = true

    // Don't launch any attack while town is threatened
    if (TownThreatened()) then
        call Sleep( 2 )
        return
    endif

    // Target Priority #1
    if (target == null) then
        set target = GetAllianceTarget()
        if (target != null) then
            set setAlly = false
        endif
    endif

    // Target Priority #2
    if (target == null) then
        set target = GetExpansionFoe()
        if (target != null) then
            set take_exp = false
        endif
    endif

    // Target Priority #3
    if (target == null) then
        set target = GetMegaTarget()
    endif

    // Target Priority #4
    if (target == null) then
        set target = GetEnemyExpansion()
    endif

    // Target Priority #5
    if (target == null) then
        set target = GetEnemyExpansion()
        if (target == null) then
            call StartGetEnemyBase(  )
            loop
                exitwhen (not WaitGetEnemyBase())
                call SuicideSleep( 1 )
            endloop
            set target = GetEnemyBase()
        endif
    endif

    // Target Priority #6
    if (target == null) then
        set target = GetCreepCamp( 0, 9, false )
    endif

    // Target Priority #7
    if (target == null) then
        set target = GetCreepCamp( 10, 100, true )
    endif

    // Attack the target and increment attack wave
    if (target != null) then
        call AttackTarget( target, setAlly )
    else
        // If no target was found, sleep a bit before trying again
        call Sleep( 20 )
    endif
endfunction

function AttackWaves takes nothing returns nothing
    //*** WAVE 1 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 2, 3, 3, ARCHER )
    call CampaignAttackerEx( 0, 1, 2, HUNTRESS )
    call Sleep( M3 ) // Waits 3 minutes before attacking
    call LaunchAttack()

    call SetBuildUpgrEx( 1, 2, 3, UPG_STR_MOON )
    call SetBuildUpgrEx( 1, 2, 3, UPG_MOON_ARMOR )
    call SetBuildUpgr( 1, UPG_GLAIVE )

    //*** WAVE 1 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 2, 3, 3, ARCHER )
    call CampaignAttackerEx( 0, 1, 2, HUNTRESS )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    call SetBuildUpgr( 1, UPG_MARKSMAN )
    call SetBuildUpgrEx( 1, 2, 3, UPG_MOON_ARMOR )
    call SetBuildUpgrEx( 1, 2, 3, UPG_HIDES )

    //*** WAVE 2 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call Sleep( M1 ) // Waits 1 minute before attacking
    call LaunchAttack()

    call SetBuildUpgrEx( 1, 2, 3, UPG_STR_MOON )

    //*** WAVE 3 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    call SetBuildUpgr( 1, UPG_MARK_CLAW )
    call SetBuildUpgrEx( 1, 2, 3, UPG_STR_MOON )
    call SetBuildUpgrEx( 1, 2, 3, UPG_MOON_ARMOR )
    call SetBuildUpgrEx( 1, 2, 3, UPG_STR_WILD )

    //*** WAVE 4 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    call SetBuildUpgrEx( 1, 2, 3, UPG_STR_MOON )
    call SetBuildUpgrEx( 1, 2, 3, UPG_MOON_ARMOR )
    call SetBuildUpgrEx( 1, 2, 3, UPG_STR_WILD )

    //*** WAVE 5 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call CampaignAttackerEx( 2, 2, 3, DRUID_TALON )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 6 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call CampaignAttackerEx( 2, 2, 3, DRUID_TALON )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 7 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call CampaignAttackerEx( 2, 2, 3, DRUID_TALON )
    call CampaignAttackerEx( 2, 2, 3, DRUID_CLAW )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 8 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call CampaignAttackerEx( 2, 2, 3, DRUID_TALON )
    call CampaignAttackerEx( 2, 2, 3, DRUID_CLAW )
    call CampaignAttackerEx( 2, 2, 3, HIPPO )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 9 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call CampaignAttackerEx( 2, 2, 3, DRUID_TALON )
    call CampaignAttackerEx( 2, 2, 3, DRUID_CLAW )
    call CampaignAttackerEx( 2, 2, 3, HIPPO )
    call CampaignAttackerEx( 2, 2, 3, MOUNTAIN_GIANT )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 10 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, ARCHER )
    call CampaignAttackerEx( 1, 2, 3, HUNTRESS )
    call CampaignAttackerEx( 1, 2, 3, DRYAD )
    call CampaignAttackerEx( 1, 2, 3, BALLISTA )
    call CampaignAttackerEx( 2, 2, 3, DRUID_TALON )
    call CampaignAttackerEx( 2, 2, 3, DRUID_CLAW )
    call CampaignAttackerEx( 2, 2, 3, HIPPO )
    call CampaignAttackerEx( 2, 2, 3, MOUNTAIN_GIANT )
    call CampaignAttackerEx( 2, 2, 3, CHIMAERA )
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    loop //Init the infinite attack loop
        //*** WAVE 11 ***
        call InitAssaultGroup()
        call CampaignAttackerEx( 20, 30, 40, ARCHER )
        call CampaignAttackerEx( 10, 20, 30, HUNTRESS )
        call CampaignAttackerEx( 10, 20, 30, DRYAD )
        call CampaignAttackerEx( 10, 20, 30, BALLISTA )
        call CampaignAttackerEx( 10, 20, 30, DRUID_TALON )
        call CampaignAttackerEx( 10, 20, 30, DRUID_CLAW )
        call CampaignAttackerEx( 10, 20, 30, HIPPO )
        call CampaignAttackerEx( 10, 20, 30, MOUNTAIN_GIANT )
        call CampaignAttackerEx( 10, 20, 30, CHIMAERA )
        call Sleep( M2 ) // Waits 2 minutes before attacking
        call LaunchAttack()
    endloop
endfunction

function main takes nothing returns nothing
    call CampaignAI( WISP, function ChooseHeroSkill )
    call ConfigureAI( )

    call BuildingStrategy( )

    call AttackWaves( )
endfunction