//===========================================================================
// Calculate the modulus/remainder of (dividend) divided by (divisor).
// Examples:  18 mod 5 = 3.  15 mod 5 = 0.  -8 mod 5 = 2.
//
function ModuloInteger takes integer dividend, integer divisor returns integer
    local integer modulus = dividend - (dividend / divisor) * divisor

    // If the dividend was negative, the above modulus calculation will
    // be negative, but within (-divisor..0).  We can add (divisor) to
    // shift this result into the desired range of (0..divisor).
    if (modulus < 0) then
        set modulus = modulus + divisor
    endif

    return modulus
endfunction

function ChooseHeroSkill takes nothing returns integer
    local integer curHero = GetHeroId()
    local integer level = GetHeroLevelAI()
    local integer mod = ModuloInteger(level, 4)
    local boolean skillUlti = ModuloInteger(level, 6) == 0 and level / 6 <= 9

    if (curHero == DEMON_HUNTER) then
        if (skillUlti) then
            return METAMORPHOSIS
        elseif (mod == 0) then
            return EVASION
        elseif (mod == 1) then
            return MANA_BURN
        elseif (mod == 2) then
            return IMMOLATION
        endif
    elseif (curHero == KEEPER) then
        if (skillUlti) then
            return TRANQUILITY
        elseif (mod == 0) then
            return THORNS_AURA
        elseif (mod == 1) then
            return FORCE_NATURE
        elseif (mod == 2) then
            return ENT_ROOTS
        endif
    elseif (curHero == MOON_CHICK) then
        if (skillUlti) then
            return STARFALL
        elseif (mod == 0) then
            return SCOUT
        elseif (mod == 1) then
            return TRUESHOT
        elseif (mod == 2) then
            return SEARING_ARROWS
        endif
    elseif (curHero == WARDEN) then
        if (skillUlti) then
            return VENGEANCE
        elseif (mod == 0) then
            return SHADOW_TOUCH
        elseif (mod == 1) then
            return FAN_KNIVES
        elseif (mod == 2) then
            return BLINK
        endif
    endif

    return 'Aamk'
endfunction

function InitHero takes nothing returns nothing
    if (GetUnitCountDone(DEMON_HUNTER) > 0) then
        set hero_id = DEMON_HUNTER
    elseif (GetUnitCountDone(KEEPER) > 0) then
        set hero_id = KEEPER
    elseif (GetUnitCountDone(MOON_CHICK) > 0) then
        set hero_id = MOON_CHICK
    elseif (GetUnitCountDone(WARDEN) > 0) then
        set hero_id = WARDEN
    endif
endfunction

function ConfigureAI takes nothing returns nothing
    call SetTargetHeroes( true )
    call SetUnitsFlee( true )
    call SetHeroesFlee( true )
    call SetPeonsRepair( true )
    call SetHeroesBuyItems( true )
    call SetHeroesTakeItems( true )
    call SetSlowChopping( false )
endfunction

//===========================================================================
// Basic attack functionality
//===========================================================================
function AttackTarget takes unit target, boolean addAlliance returns nothing
    if (target == null) then
        return
    endif
    if (addAlliance) then
        call SetAllianceTarget( target )
    endif
    call FormGroup( 3, true )
    call AttackMoveKillA( target )
    if (not addAlliance) then
        call SetAllianceTarget( null )
    endif
endfunction

//===========================================================================
// Initiates an attack based on target priorities
//===========================================================================
function LaunchAttack takes nothing returns nothing
    local unit target = null
    local boolean setAlly = true

    // Don't launch any attack while town is threatened
    if (TownThreatened()) then
        call Sleep( 2 )
        return
    endif

    // Target Priority #1
    if (target == null) then
        set target = GetAllianceTarget()
        if (target != null) then
            set setAlly = false
        endif
    endif

    // Target Priority #2
    if (target == null) then
        set target = GetExpansionFoe()
        if (target != null) then
            set take_exp = false
        endif
    endif

    // Target Priority #3
    if (target == null) then
        set target = GetMegaTarget()
    endif

    // Target Priority #4
    if (target == null) then
        set target = GetEnemyExpansion()
    endif

    // Target Priority #5
    if (target == null) then
        set target = GetEnemyExpansion()
        if (target == null) then
            call StartGetEnemyBase(  )
            loop
                exitwhen (not WaitGetEnemyBase())
                call SuicideSleep( 1 )
            endloop
            set target = GetEnemyBase()
        endif
    endif

    // Target Priority #6
    if (target == null) then
        set target = GetCreepCamp( 0, 9, false )
    endif

    // Target Priority #7
    if (target == null) then
        set target = GetCreepCamp( 10, 100, true )
    endif

    // Attack the target and increment attack wave
    if (target != null) then
        call AttackTarget( target, setAlly )
    else
        // If no target was found, sleep a bit before trying again
        call Sleep( 20 )
    endif
endfunction

function AttackWaves takes nothing returns nothing
    loop
        call InitAssaultGroup()
        call CampaignAttackerEx( 1, 1, 1, hero_id )
        call Sleep( M2 ) // Waits 2 minutes before attacking
        call LaunchAttack()
    endloop
endfunction

function main takes nothing returns nothing
    call CampaignAI( 0, function ChooseHeroSkill )
    call ConfigureAI( )
    call InitHero( )

    call BuildingStrategy( )

    call AttackWaves( )
endfunction