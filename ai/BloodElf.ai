globals
	constant integer BLOOD_ELF_ARCANE_SACNTUM                     = 'h00E' // arcane
	constant integer BLOOD_ELF_ARCANE_VAULT                       = 'h00H' // shop
	constant integer BLOOD_ELF_BARRACKS                           = 'h00C' // barracks
	constant integer BLOOD_ELF_BLACK_SMITH                        = 'h00D' // blacksmith
	constant integer BLOOD_ELF_CASTLE                             = 'h00B' // castle
	constant integer BLOOD_ELF_KEEP                               = 'h00A' // keep
	constant integer BLOOD_ELF_LUMBER_MILL                        = 'h00F' // lumber mill
	constant integer BLOOD_ELF_TOWN_HALL                          = 'h009' // town hall
	constant integer BLOOD_ELF_SCOUT_TOWER                        = 'h00I' // tower
	constant integer BLOOD_ELF_WORKSHOP                           = 'h013' // workshop
	
	constant integer UPG_BLOOD_ELF_BREEDING                       = 'R000'
	constant integer UPG_BLOOD_ELF_BOWS                           = 'R009'
	constant integer UPG_BLOOD_ELF_WOOD                           = 'R006'
	constant integer UPG_BLOOD_ELF_MASONRY                        = 'R003'
	constant integer UPG_BLOOD_ELF_MELEE                          = 'R001'
	constant integer UPG_BLOOD_ELF_ARMOR                          = 'R002'
	constant integer UPG_BLOOD_ELF_MARKSMAN                       = 'R00A'
	constant integer UPG_BLOOD_ELF_MOON_ARMOR                     = 'R004'
	constant integer UPG_BLOOD_ELF_PRAYING                        = 'R007'
	constant integer UPG_BLOOD_ELF_STR_MOON                       = 'R008'
	constant integer UPG_BLOOD_ELF_LEATHER                        = 'R005'
	constant integer UPG_BLOOD_ELF_BURNING_OIL                    = 'R00Y'
	constant integer UPG_BLOOD_ELF_IMPROVED_SIEGE                 = 'R00Z'
	
	constant integer UPG_TEMPLE_OF_DEMIGODS_BLUEPRINTS            = 'R00B'
	constant integer UPG_EVOLUTION                                = 'R00U'

    constant integer BLOOD_ELF_PRIEST                             = 'h00G'
	constant integer BLOOD_ELF_LIEUTENANT                         = 'nbel'
	constant integer BLOOD_ELF_WAGON                              = 'h012'
	constant integer BLOOD_ELF_BALLISTA                           = 'e00C'
	constant integer BLOOD_ELF_CAGE                               = 'e00D'

    constant integer BLOOD_ELF_CITIZEN_MALE                       = 'n00K'
    constant integer BLOOD_ELF_CITIZEN_FEMALE                     = 'n00L'
    constant integer BLOOD_ELF_HOUSING                            = 'h00T'
	constant integer PORTAL                                       = 'h014'
	
	constant integer GRYPHON_MOUNT                                   = 'h004'
	constant integer WYVERN_MOUNT                                    = 'o003'
	constant integer FROST_WYRM_MOUNT                                = 'u004'
	constant integer CHIMAERA_MOUNT                                  = 'e005'
	constant integer NETHER_DRAKE_MOUNT                              = 'n007'
endglobals

//===========================================================================
// Calculate the modulus/remainder of (dividend) divided by (divisor).
// Examples:  18 mod 5 = 3.  15 mod 5 = 0.  -8 mod 5 = 2.
//
function ModuloInteger takes integer dividend, integer divisor returns integer
    local integer modulus = dividend - (dividend / divisor) * divisor

    // If the dividend was negative, the above modulus calculation will
    // be negative, but within (-divisor..0).  We can add (divisor) to
    // shift this result into the desired range of (0..divisor).
    if (modulus < 0) then
        set modulus = modulus + divisor
    endif

    return modulus
endfunction

function ChooseHeroSkill takes nothing returns integer
    local integer curHero = GetHeroId()
    local integer level = GetHeroLevelAI()
    local integer mod = ModuloInteger(level, 4)
    local boolean skillUlti = ModuloInteger(level, 6) == 0 and level / 6 <= 9

    if (curHero == BLOOD_MAGE) then
        if (skillUlti) then
            return SUMMON_PHOENIX
        elseif (mod == 0) then
            return BANISH
        elseif (mod == 1) then
            return FLAME_STRIKE
        elseif (mod == 2) then
            return SIPHON_MANA
        endif
    endif

    return 'Aamk'
endfunction

function InitHero takes nothing returns nothing
    if (GetUnitCountDone(BLOOD_MAGE) > 0) then
        set hero_id = BLOOD_MAGE
    endif
endfunction

function UpdateWorkers takes nothing returns nothing
	loop
		call SetGoldPeons(5 + GetUnitCount(BLOOD_ELF_HOUSING) * 3)
		if ((GetUnitCount(BLOOD_ELF_KEEP) == 0) and (GetUnitCount(BLOOD_ELF_CASTLE) == 0)) then
			call SetWoodPeons(3)
		elseif (GetUnitCount(BLOOD_ELF_CASTLE) == 0) then
			call SetWoodPeons(4)
		else
			call SetWoodPeons(5)
		endif
		call Sleep(1)
	endloop
endfunction

function ConfigureAI takes nothing returns nothing
    call SetTargetHeroes( true )
    call SetUnitsFlee( true )
    call SetHeroesFlee( true )
    call SetPeonsRepair( true )
    call SetHeroesBuyItems( true )
    call SetHeroesTakeItems( true )
    call SetSlowChopping( false )
	call SetWatchMegaTargets( true )
	call SetIgnoreInjured( false )
	call SetGroupsFlee( true )
	//call SetCaptainChanges ( true )
	call SetSmartArtillery ( true )
endfunction

function BuildingStrategy takes nothing returns nothing
    // **********************************
    // *      Building Strategy         *
    // **********************************
    // Tier 1 Buildings
	call SetReplacements( 1, 2, 3 )
	if (((GetUnitCount(BLOOD_ELF_KEEP) == 0) and (GetUnitCount(BLOOD_ELF_CASTLE) == 0))) then
		call SetBuildUnit( 1, BLOOD_ELF_TOWN_HALL )
	endif
	call SetBuildUnit( 17, BLOOD_PEASANT )
	call SetBuildUnit( 2, BLOOD_ELF_CITIZEN_MALE )
	call SetBuildUnit( 3, HOUSE )
	call SetBuildUnit( 2, BLOOD_ELF_HOUSING )
	call SetBuildUnit( 2, BLOOD_ELF_BARRACKS )
	call SetBuildUnit( 1, HUMAN_ALTAR )
	call SetBuildUnit( 1, BLOOD_ELF_LUMBER_MILL )
	call SetBuildUnit( 1, BLOOD_ELF_BLACK_SMITH )
	call SetBuildUnit( 1, BLOOD_ELF_ARCANE_VAULT )
	
	call SetBuildUpgr( 1, UPG_DEFEND )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_MELEE )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_ARMOR )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_MASONRY )

    // Tier 2 buildings
	if (((GetUnitCount(BLOOD_ELF_CASTLE) == 0))) then
		call SetBuildUnit( 1, BLOOD_ELF_KEEP )
	endif
	call SetBuildUnit( 1, BLOOD_ELF_LUMBER_MILL )
	call SetBuildUnit( 1, BLOOD_ELF_BLACK_SMITH )
	call SetBuildUnit( 1, BLOOD_ELF_ARCANE_SACNTUM )
	call SetBuildUnit( 4, HOUSE )
	call SetBuildUnit( 2, BLOOD_ELF_SCOUT_TOWER )
	call SetBuildUnit( 5, BLOOD_PEASANT )
	call SetBuildUnit( 2, BLOOD_ELF_CITIZEN_FEMALE )
	
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_MELEE )
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_ARMOR )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_MOON_ARMOR )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_STR_MOON )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_WOOD )
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_MASONRY )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_PRAYING )
	call SetBuildUpgr( 1, UPG_SORCERY )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_BOWS )
	
    // Tier 3 buildings
	call SetBuildUnit( 1, BLOOD_ELF_CASTLE )
	call SetBuildUnit( 5, HOUSE )
	call SetBuildUnit( 5, BLOOD_PEASANT )
	call SetBuildUnit( 1, ELF_GUARD_TOWER )
	call SetBuildUnit( 1, ARCANE_TOWER )
	call SetBuildUnit( 1, BLOOD_ELF_WORKSHOP )
	
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_WOOD )
	call SetBuildUpgr( 3, UPG_BLOOD_ELF_MELEE )
	call SetBuildUpgr( 3, UPG_BLOOD_ELF_ARMOR )
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_MOON_ARMOR )
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_STR_MOON )
	call SetBuildUpgr( 3, UPG_BLOOD_ELF_MASONRY )
	call SetBuildUpgr( 2, UPG_BLOOD_ELF_PRAYING )
	call SetBuildUpgr( 2, UPG_SORCERY )
	call SetBuildUpgr( 3, UPG_BLOOD_ELF_IMPROVED_SIEGE )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_BREEDING )
	call SetBuildUpgr( 1, UPG_BLOOD_ELF_MARKSMAN )
	call SetBuildUpgr( 1, UPG_CONT_MAGIC )
	call SetBuildUpgr( 1, UPG_SENTINEL )
	call SetBuildUpgr( 1, UPG_CLOUD )
	call SetBuildUpgr( 1, UPG_TEMPLE_OF_DEMIGODS_BLUEPRINTS )
	call SetBuildUpgr( 100, UPG_EVOLUTION )
    // **********************************
    // *    End Building Strategy       *
    // **********************************
endfunction

function AttackWithMount takes nothing returns nothing
	if (GetUnitCount(GRYPHON_MOUNT) > 0) then
		call CampaignAttacker( 0, 1, GRYPHON_MOUNT )
	endif
	if (GetUnitCount(WYVERN_MOUNT) > 0) then
		call CampaignAttacker( 0, 1, WYVERN_MOUNT )
	endif
	if (GetUnitCount(FROST_WYRM_MOUNT) > 0) then
		call CampaignAttacker( 0, 1, FROST_WYRM_MOUNT )
	endif
	if (GetUnitCount(CHIMAERA_MOUNT) > 0) then
		call CampaignAttacker( 0, 1, CHIMAERA_MOUNT )
	endif
	if (GetUnitCount(NETHER_DRAKE_MOUNT) > 0) then
		call CampaignAttacker( 0, 1, NETHER_DRAKE_MOUNT )
	endif
endfunction

function AddStandardUnitsToAttackWave takes nothing returns nothing
	call PurchaseZeppelin()
	call AttackWithMount()
    call CampaignAttacker( 0, 1, hero_id )
endfunction

//===========================================================================
// Basic attack functionality
//===========================================================================
function AttackTarget takes unit target, boolean addAlliance returns nothing
    if (target == null) then
        return
    endif
    if (addAlliance) then
        call SetAllianceTarget( target )
    endif
    call FormGroup( 3, true )
    call AttackMoveKillA( target )
    if (not addAlliance) then
        call SetAllianceTarget( null )
    endif
endfunction

//===========================================================================
// Initiates an attack based on target priorities
//===========================================================================
function LaunchAttack takes nothing returns nothing
    local unit target = null
    local boolean setAlly = true

    // Don't launch any attack while town is threatened
    if (TownThreatened()) then
        call Sleep( 2 )
        return
    endif

    // Target Priority #1
    if (target == null) then
        set target = GetAllianceTarget()
        if (target != null) then
            set setAlly = false
        endif
    endif

    // Target Priority #2
    if (target == null) then
        set target = GetExpansionFoe()
        if (target != null) then
            set take_exp = false
        endif
    endif

    // Target Priority #3
    if (target == null) then
        set target = GetMegaTarget()
    endif

    // Target Priority #4
    if (target == null) then
        set target = GetEnemyExpansion()
    endif

    // Target Priority #5
    if (target == null) then
        set target = GetEnemyExpansion()
        if (target == null) then
            call StartGetEnemyBase(  )
            loop
                exitwhen (not WaitGetEnemyBase())
                call SuicideSleep( 1 )
            endloop
            set target = GetEnemyBase()
        endif
    endif

    // Target Priority #6
    if (target == null) then
        set target = GetCreepCamp( 0, 9, false )
    endif

    // Target Priority #7
    if (target == null) then
        set target = GetCreepCamp( 10, 100, true )
    endif

    // Attack the target and increment attack wave
    if (target != null) then
        call AttackTarget( target, setAlly )
    else
        // If no target was found, sleep a bit before trying again
        call Sleep( 20 )
    endif
endfunction

function AttackWaves takes nothing returns nothing
    //*** WAVE 1 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 2, 3, 3, HIGH_SWORDMAN )
    call CampaignAttackerEx( 0, 1, 2, HIGH_ARCHER )
    call AddStandardUnitsToAttackWave()
    call Sleep( M3 ) // Waits 3 minutes before attacking
    call LaunchAttack()

    //*** WAVE 2 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 2, 3, 3, HIGH_SWORDMAN )
    call CampaignAttackerEx( 0, 1, 2, HIGH_ARCHER )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 3 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call AddStandardUnitsToAttackWave()
    call Sleep( M1 ) // Waits 1 minute before attacking
    call LaunchAttack()

    //*** WAVE 4 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 5 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 6 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
    call CampaignAttackerEx( 1, 2, 3, SORCERESS )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 7 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
	call CampaignAttackerEx( 1, 2, 3, SORCERESS )
    call CampaignAttackerEx( 2, 2, 3, HUMAN_DRAGON_HAWK )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 8 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
	call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
	call CampaignAttackerEx( 1, 2, 3, SORCERESS )
    call CampaignAttackerEx( 2, 2, 3, HUMAN_DRAGON_HAWK )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 9 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
	call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
	call CampaignAttackerEx( 1, 2, 3, SORCERESS )
    call CampaignAttackerEx( 2, 2, 3, HUMAN_DRAGON_HAWK )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 10 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
	call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
	call CampaignAttackerEx( 1, 2, 3, SORCERESS )
    call CampaignAttackerEx( 2, 2, 3, HUMAN_DRAGON_HAWK )
	call CampaignAttackerEx( 2, 2, 3, BLOOD_ELF_LIEUTENANT )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    //*** WAVE 11 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3, 4, 4, HIGH_SWORDMAN )
    call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
	call CampaignAttackerEx( 1, 2, 3, HIGH_ARCHER )
    call CampaignAttackerEx( 1, 2, 3, SPELL_BREAKER )
    call CampaignAttackerEx( 1, 2, 3, BLOOD_ELF_PRIEST )
	call CampaignAttackerEx( 1, 2, 3, SORCERESS )
    call CampaignAttackerEx( 2, 2, 3, HUMAN_DRAGON_HAWK )
	call CampaignAttackerEx( 2, 2, 3, BLOOD_ELF_LIEUTENANT )
    call AddStandardUnitsToAttackWave()
    call Sleep( M2 ) // Waits 2 minutes before attacking
    call LaunchAttack()

    loop //Init the infinite attack loop
		//*** WAVE 12 ***
		call InitAssaultGroup()
		call CampaignAttackerEx( 4, 8, 12, HIGH_SWORDMAN )
		call CampaignAttackerEx( 4, 8, 12, HIGH_ARCHER )
		call CampaignAttackerEx( 4, 8, 12, HIGH_ARCHER )
		call CampaignAttackerEx( 3, 4, 5, SPELL_BREAKER )
		call CampaignAttackerEx( 3, 4, 5, BLOOD_ELF_PRIEST )
		call CampaignAttackerEx( 3, 4, 5, SORCERESS )
		call CampaignAttackerEx( 3, 4, 5, HUMAN_DRAGON_HAWK )
		call CampaignAttackerEx( 3, 4, 5, BLOOD_ELF_LIEUTENANT )
		call CampaignAttackerEx( 3, 4, 5, BLOOD_ELF_WAGON )
		call CampaignAttackerEx( 3, 4, 5, BLOOD_ELF_BALLISTA )
		call CampaignAttackerEx( 3, 4, 5, BLOOD_ELF_CAGE )
		call AddStandardUnitsToAttackWave()
		call Sleep( M2 ) // Waits 2 minutes before attacking
		call LaunchAttack()
    endloop
endfunction

function main takes nothing returns nothing
    call CampaignAI( HOUSE, function ChooseHeroSkill )
    call ConfigureAI( )
    call InitHero( )
	call StartThread(function UpdateWorkers)

    call BuildingStrategy( )

    call AttackWaves( )
endfunction