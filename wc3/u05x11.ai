//==================================================================================================
//  $Id: u05x11.ai,v 1.18.2.1 2003/05/09 09:17:05 abond Exp $
//==================================================================================================
globals
    player user = PlayerEx(4)

    integer delay       = M5          // normal & easy delay
    integer hard_delay  = M4          // hard delay
    integer fast_delay  = M3          // hard delay after final signal
endglobals

//--------------------------------------------------------------------------------------------------
//  check_base_deaths
//--------------------------------------------------------------------------------------------------
function check_base_deaths takes nothing returns nothing
    if difficulty == HARD then
        return
    endif
    loop
        exitwhen CommandsWaiting() != 0 
        call Trace("check_base_deaths waiting for signal...\n")
        call Sleep(10)
    endloop
    call PopLastCommand()
    set delay = fast_delay
endfunction

//--------------------------------------------------------------------------------------------------
//  upgrade_towers
//--------------------------------------------------------------------------------------------------
function upgrade_towers takes nothing returns nothing
    local integer count
    loop
        set count = TownCountDone(ZIGGURAT_1)
        if count > 0 then
            call SetProduce(count,ZIGGURAT_2,-1)
        endif
        call Sleep(1)
    endloop
endfunction

//--------------------------------------------------------------------------------------------------
//  main
//--------------------------------------------------------------------------------------------------
function main takes nothing returns nothing

    call CampaignAI(ZIGGURAT_1,null)
    call SetReplacements(2,2,3)
    set campaign_wood_peons = 2

    if difficulty == HARD then
        set delay = hard_delay
    endif

    call SetBuildUnitEx( 1, 1, 1, ACOLYTE           )
    call SetBuildUnitEx( 1, 1, 1, NECROPOLIS_1      )
    call SetBuildUnitEx( 0, 0, 1, UNDEAD_MINE       )
    call SetBuildUnitEx( 0, 0, 1, CRYPT             )
    call SetBuildUnitEx( 0, 0, 8, ZIGGURAT_1        )
    call SetBuildUnitEx( 0, 0, 1, GRAVEYARD         )
    call SetBuildUnitEx( 0, 0, 1, UNDEAD_ALTAR      )
    call SetBuildUnitEx( 1, 1, 1, NECROPOLIS_2      )
    call SetBuildUnitEx( 0, 0, 1, SLAUGHTERHOUSE    )
    call SetBuildUnitEx( 0, 0, 1, DAMNED_TEMPLE     )
    call SetBuildUnitEx( 1, 1, 1, NECROPOLIS_3      )
    call SetBuildUnitEx( 5, 5, 5, ACOLYTE           )

    call WaitForSignal()

    // SUICIDE WAVES - Occur after timer runs out
    call SuicideOnce( 3, 3, 3, GHOUL        )
    call SuicideOnce( 1, 1, 1, ABOMINATION  )
    call SuicideOnce( 2, 2, 2, CRYPT_FIEND  )
    call SuicideOnce( 2, 2, 2, GARGOYLE     )
    call SuicideOnce( 2, 2, 2, NECRO        )

    call WaitForSignal()

    // ENABLE DEFENDERS - Occurs after Suicide is over
    call CampaignDefenderEx( 0, 0, 1, ABOMINATION   )
    call CampaignDefenderEx( 1, 1, 1, NECRO         )
    call CampaignDefenderEx( 1, 1, 1, GARGOYLE      )
    call CampaignDefenderEx( 0, 0, 1, CRYPT_FIEND   )

    call StartThread(function upgrade_towers)

    call WaitForSignal()

    call StartThread(function check_base_deaths)

    //*** WAVE 1 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 6,6,8, GHOUL       )
    call SuicideOnPlayerEx(M5,M5,M5,user)

    call SetBuildUpgrEx( 0,0,1, UPG_UNHOLY_STR  )
    call SetBuildUpgrEx( 0,1,1, UPG_CR_ATTACK   )

    //*** WAVE 2 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 1,1,2, MEAT_WAGON  )
    call CampaignAttackerEx( 5,5,7, GHOUL       )
    call CampaignAttackerEx( 1,1,2, NECRO       )
    call SuicideOnPlayer(delay,user)

    call SetBuildUpgrEx( 0,0,1, UPG_UNHOLY_ARMOR)
    call SetBuildUpgrEx( 0,0,1, UPG_CR_ARMOR    )

    //*** WAVE 3 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 6,6,8, GHOUL       )
    call CampaignAttackerEx( 4,4,5, NECRO       )
    call SuicideOnPlayer(delay,user)
    
    call SetBuildUpgrEx( 0,0,1, UPG_GHOUL_FRENZY)
    call SetBuildUpgrEx( 1,1,1, UPG_NECROS      )
    
    //*** WAVE 4 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 4,4,5, ABOMINATION )
    call SuicideOnPlayer(delay,user)

    call SetBuildUpgrEx( 1,1,2, UPG_UNHOLY_STR  )
    call SetBuildUpgrEx( 1,1,2, UPG_CR_ATTACK   )

    //*** WAVE 5 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 6,6,6, NECRO       )
    call CampaignAttackerEx( 2,2,3, ABOMINATION )
    call CampaignAttackerEx( 1,1,2, MEAT_WAGON  )
    call SuicideOnPlayer(delay,user)

    call SetBuildUpgrEx( 1,1,2, UPG_UNHOLY_ARMOR)
    call SetBuildUpgrEx( 1,1,2, UPG_CR_ARMOR    )
    call SetBuildUpgrEx( 2,2,2, UPG_NECROS      )

    //*** WAVE 6 ***
    call InitAssaultGroup()
    call CampaignAttackerEx( 3,3,5, CRYPT_FIEND )
    call CampaignAttackerEx( 3,3,5, ABOMINATION )
    call CampaignAttackerEx( 1,1,2, MEAT_WAGON  )
    call SuicideOnPlayer(delay,user)

    call SetBuildUpgrEx( 2,2,3, UPG_UNHOLY_STR  )
    call SetBuildUpgrEx( 2,2,3, UPG_CR_ATTACK   )
    call SetBuildUpgrEx( 1,1,1, UPG_GHOUL_FRENZY)
    call SetBuildUpgrEx( 1,1,1, UPG_PLAGUE      )

    //*** WAVE 7+ ***
    loop
        call InitAssaultGroup()
        call CampaignAttackerEx( 5,5,8, NECRO       )
        call CampaignAttackerEx( 3,3,5, CRYPT_FIEND )
        call SuicideOnPlayer(delay,user)

        call InitAssaultGroup()
        call CampaignAttackerEx( 3,3,5, CRYPT_FIEND )
        call CampaignAttackerEx( 2,2,4, NECRO       )
        call CampaignAttackerEx( 2,2,3, ABOMINATION )
        call CampaignAttackerEx( 1,1,2, MEAT_WAGON  )
        call SuicideOnPlayer(delay,user)

        call InitAssaultGroup()
        call CampaignAttackerEx( 1,1,2, MEAT_WAGON  )
        call CampaignAttackerEx( 5,5,6, NECRO       )
        call CampaignAttackerEx( 1,1,3, ABOMINATION )
        call SuicideOnPlayer(delay,user)

        call InitAssaultGroup()
        call CampaignAttackerEx( 4,4,6, ABOMINATION )
        call CampaignAttackerEx( 2,2,3, CRYPT_FIEND )
        call SuicideOnPlayer(delay,user)
    endloop
endfunction

